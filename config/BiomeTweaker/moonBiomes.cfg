# new biomes
# stages: PRE_INIT, BIOME_REGISTRY, INIT, POST_INIT, FINISHED_LOAD, SERVER_STARTING, SERVER_STARTED
# placement stages: BIOME_BLOCKS, PRE_POPULATE, POST_POPULATE, PRE_DECORATE, PRE_ORES, POST_ORES, POST_DECORATE
Tweaker.setScriptStage("BIOME_REGISTRY")

Tweaker.createBiome("frost_forest")
Tweaker.createBiome("tar_pits")
Tweaker.createBiome("shattered_skies")

# id spec
#moon = forBiomes(45)
#darkMoon = forBiomes(54)
frostForest = forBiomes("biometweaker:frost_forest")
tarPits = forBiomes("biometweaker:tar_pits")
shatteredSkies = forBiomes("biometweaker:shattered_skies")

# frost forest defintions
# copy base moon settings for now
# have to do this manually b/c AR registers things poorly
frostForest.set("name", "Frost Forest")
frostForest.set("height", 1)
frostForest.set("heightVariation", 0.2)
frostForest.set("topBlock", "minecraft:snow")
frostForest.set("fillerBlock", "advancedrocketry:moonturf")
frostForest.set("waterColor", "16777215")
frostForest.set("temperature", 0.2)
frostForest.set("humidity", 0)
frostForest.set("enableRain", false)
frostForest.set("genVillages", false)
frostForest.set("genStrongholds", false)
frostForest.set("genInitialSnow", false)
frostForest.addDicTypes("COLD", "FOREST", "SNOWY", "MOON")

# tar pit defintions
tarPits.set("name", "Tar Pits")
tarPits.set("height", 0.5)
tarPits.set("heightVariation", 0.01)
tarPits.set("topBlock", "forestry:bog_earth")
tarPits.set("fillerBlock", "advancedrocketry:moonturf_dark")
tarPits.set("waterColor", "16777215")
tarPits.set("temperature", 0.2)
tarPits.set("humidity", 0)
tarPits.set("enableRain", false)
tarPits.set("genVillages", false)
tarPits.set("genStrongholds", false)
tarPits.addDicTypes("MOON", "OILY", "COLD")

# shattered skies defintions
shatteredSkies.set("name", "Shattered Skies")
shatteredSkies.set("height", 0.5)
shatteredSkies.set("heightVariation", 0.01)
shatteredSkies.set("topBlock", "minecraft:air")
shatteredSkies.set("fillerBlock", "minecraft:air")
shatteredSkies.set("waterColor", "16777215")
shatteredSkies.set("temperature", 0.3)
shatteredSkies.set("humidity", 0)
shatteredSkies.set("enableRain", false)
shatteredSkies.set("genVillages", false)
shatteredSkies.set("genStrongholds", false)
shatteredSkies.addDicTypes("MOON", "SKY")
# set stage to BIOME_BLOCKS to replace stone in skies biome
Tweaker.setPlacementStage("BIOME_BLOCKS")
shatteredSkies.registerGenBlockRep("minecraft:stone", "minecraft:air")
shatteredSkies.removeDecoration("SHROOM")
shatteredSkies.removeFeature("LAVA")
Tweaker.setPlacementStage("POST_POPULATE")
shatteredSkies.registerGenBlockRep("minecraft:lava", "minecraft:air")
shatteredSkies.registerGenBlockRep("minecraft:bedrock", "minecraft:air")
shatteredSkies.registerGenBlockRep("minecraft:brown_mushroom", "minecraft:air")
shatteredSkies.registerGenBlockRep("minecraft:red_mushroom", "minecraft:air")